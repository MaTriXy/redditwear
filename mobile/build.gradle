apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    signingConfigs {
        release
    }
    defaultConfig {
        applicationId "com.emmaguy.todayilearned"
        minSdkVersion 18
        targetSdkVersion 22
        versionCode 36
        versionName "3.0.4a"
        resValue "string", "client_id", "placeholderId"
        resValue "string", "google_analytics_id", "placeholderId"
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
        debug {
            testCoverageEnabled true
        }
    }
    lintOptions {
        disable 'Overdraw', 'InvalidPackage', 'UnusedResources'
    }
}

repositories {
    maven { url 'https://repo.commonsware.com.s3.amazonaws.com' }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':sharedlib')

    compile 'io.reactivex:rxjava:1.0.12'
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'net.danlew:android.joda:2.7.2'
    compile 'com.commonsware.cwac:wakeful:1.0.5'

    // Dagger
    compile 'com.google.dagger:dagger:2.0.1'
    apt 'com.google.dagger:dagger-compiler:2.0.1'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    // Logging
    compile 'com.jakewharton.timber:timber:3.1.0'

    // Network
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.3.0' // https://github.com/square/retrofit/issues/854

    // UI
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile('de.psdev.licensesdialog:licensesdialog:1.5.0') {
        exclude module: 'support-v4'
    }

    // Google Analytics
    compile 'com.google.android.gms:play-services-analytics:7.5.0'
    compile 'com.google.android.gms:play-services-wearable:7.5.0'
    wearApp project(':wear')

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.0.7-beta'
}

def Properties props = new Properties()
if (project.hasProperty("signing.properties") && file(project.property("signing.properties")).canRead()) {
    props.load(new FileInputStream(file(project.property("signing.properties"))))

    if (props != null && props.containsKey('keystore') && props.containsKey('keystore.password') &&
            props.containsKey('keyalias') && props.containsKey('keyalias.password')) {
        android.signingConfigs.release.storeFile = file(props['keystore'])
        android.signingConfigs.release.storePassword = props['keystore.password']
        android.signingConfigs.release.keyAlias = props['keyalias']
        android.signingConfigs.release.keyPassword = props['keyalias.password']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }

    if (props != null && props.containsKey('clientid')) {
        android.defaultConfig.resValue "string", "client_id", props['clientid']
    } else {
        println 'Did not find client id'
    }
    if (props != null && props.containsKey('googleanalytics')) {
        android.defaultConfig.resValue "string", "google_analytics_id", props['googleanalytics']
    } else {
        println 'Did not find GA tracker'
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}

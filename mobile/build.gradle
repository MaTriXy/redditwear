apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

Properties props = new Properties()
props.load(new FileInputStream(file(project.property("signing.properties"))))

android {
    compileSdkVersion 22
    buildToolsVersion '21.1.2'

    signingConfigs {
        release {
            storeFile file(props['keystore'])
            storePassword props['keystore.password']
            keyAlias props['keyalias']
            keyPassword props['keyalias.password']
        }
    }
    defaultConfig {
        applicationId "com.emmaguy.todayilearned"
        signingConfig signingConfigs.release
        minSdkVersion 18
        targetSdkVersion 22
        versionCode 33
        versionName "3.0.2"
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

repositories {
    maven { url 'https://repo.commonsware.com.s3.amazonaws.com' }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':sharedlib')

    compile 'io.reactivex:rxjava:1.0.12'
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'net.danlew:android.joda:2.7.2'
    compile 'com.commonsware.cwac:wakeful:1.0.5'

    // Dagger
    compile 'com.google.dagger:dagger:2.0.1'
    apt 'com.google.dagger:dagger-compiler:2.0.1'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    // Network
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.3.0' // https://github.com/square/retrofit/issues/854

    // UI
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile('de.psdev.licensesdialog:licensesdialog:1.5.0') {
        exclude module: 'support-v4'
    }

    // Google Analytics
    compile 'com.google.android.gms:play-services:7.3.0'
    compile 'com.google.android.gms:play-services-wearable:7.3.0'
    wearApp project(':wear')

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.0.7-beta'
}
